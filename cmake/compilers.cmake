
# Print some information
message(STATUS "CMAKE_CXX_COMPILER: ${CMAKE_CXX_COMPILER}")
message(STATUS "CMAKE_CXX_COMPILER_ID: ${CMAKE_CXX_COMPILER_ID}")
message(STATUS "CMAKE_CXX_COMPILER_VERSION: ${CMAKE_CXX_COMPILER_VERSION}")
message(STATUS "CMAKE_INSTALL_PREFIX: ${CMAKE_INSTALL_PREFIX}")
if (DEFINED MSVC_VERSION)
    message(STATUS "MSVC_VERSION: ${MSVC_VERSION}")
endif()

# TheSuperHackers @build JohnsterID 05/01/2026 Add MinGW-w64 detection and configure compiler flags
# Detect MinGW-w64
if(MINGW)
    message(STATUS "MinGW-w64 detected")
    set(IS_MINGW_BUILD TRUE)
else()
    set(IS_MINGW_BUILD FALSE)
endif()

# Set variable for VS6 to handle special cases.
if (DEFINED MSVC_VERSION AND MSVC_VERSION LESS 1300)
    set(IS_VS6_BUILD TRUE)
else()
    set(IS_VS6_BUILD FALSE)
endif()

# Make release builds have debug information too.
if(MSVC)
    # Create PDB for Release as long as debug info was generated during compile.
    string(APPEND CMAKE_EXE_LINKER_FLAGS_RELEASE " /DEBUG /OPT:REF /OPT:ICF")
    string(APPEND CMAKE_SHARED_LINKER_FLAGS_RELEASE " /DEBUG /OPT:REF /OPT:ICF")
    
    # /INCREMENTAL:NO prevents PDB size bloat in Debug configuration(s).
    add_link_options("/INCREMENTAL:NO")
else()
    # We go a bit wild here and assume any other compiler we are going to use supports -g for debug info.
    # Add debug symbols to Release builds for crash dump analysis, profiling, and post-mortem debugging.
    # For MinGW, symbols will be stripped to separate .debug files (matching MSVC PDB workflow).
    string(APPEND CMAKE_CXX_FLAGS_RELEASE " -g")
    string(APPEND CMAKE_C_FLAGS_RELEASE " -g")
endif()

set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)  # Ensures only ISO features are used

if (NOT IS_VS6_BUILD)
    if (MSVC)
        # Multithreaded build.
        add_compile_options(/MP)
        # Enforce strict __cplusplus version
        add_compile_options(/Zc:__cplusplus)
        # TheSuperHackers @build bobtista 10/06/2026 Emit SSE2 scalar floating-point instead of x87
        # on 32-bit builds. x87 keeps 80-bit extended-precision intermediates and setFPMode()'s
        # _PC_24 control word forces single-precision rounding, so double-precision math (e.g.
        # gm_atan2) diverges from the macOS/arm64 NEON build and breaks cross-platform deterministic
        # lockstep. SSE2 doubles are true IEEE-754 64-bit, matching arm64. (x64 already uses SSE2.)
        if (CMAKE_SIZEOF_VOID_P EQUAL 4)
            add_compile_options(/arch:SSE2)
        endif()
        # Keep MSVC from contracting/reassociating FP so it matches clang -ffp-contract=off.
        add_compile_options(/fp:precise)
    else()
        add_compile_options(-Wsuggest-override)
        # Prevent FMA contraction (a*b+c -> fmadd) which skips intermediate
        # rounding and breaks cross-platform deterministic math parity with MSVC (/fp:precise).
        add_compile_options(-ffp-contract=off)

        # TheSuperHackers @build githubawn 29/07/2026 Emscripten (WebAssembly) settings
        # that every target and dependency has to share, so they live here rather than on
        # the game executable. Scoped to EMSCRIPTEN, so no other platform is affected.
        if(EMSCRIPTEN)
            # The game's address space grows well past the default 16MB heap; cap it at
            # the wasm32 ceiling.
            add_link_options(-sALLOW_MEMORY_GROWTH=1)
            add_link_options(-sMAXIMUM_MEMORY=4194304000)
            # Real threads (Web Workers) for the engine's worker threads (texture loader,
            # audio, file preload). -pthread has to be on compile AND link for every
            # target, including SDL3/bgfx, hence the global add here. The page must be
            # cross-origin isolated (COOP/COEP) to get SharedArrayBuffer.
            add_compile_options(-pthread)
            add_link_options(-pthread)
            # The engine uses C++ exceptions for INI/asset error handling. Emscripten
            # disables exception catching by default (any throw aborts with "exception
            # catching is not enabled"). -fexceptions is the Asyncify-compatible form;
            # -fwasm-exceptions conflicts with Asyncify.
            add_compile_options(-fexceptions)
            add_link_options(-fexceptions)
        endif()
    endif()
else()
    if(RTS_BUILD_OPTION_VC6_FULL_DEBUG)
        set_property(GLOBAL PROPERTY JOB_POOLS compile=1 link=1)
    else()
        # Define two pools: 'compile' with plenty of slots, 'link' with just one
        set_property(GLOBAL PROPERTY JOB_POOLS compile=0 link=1)
    endif()

    # Tell CMake that all compile steps go into 'compile'
    set(CMAKE_JOB_POOL_COMPILE compile)
    # and all link steps go into 'link' (so only one link ever runs since vc6 can't handle multithreaded linking)
    set(CMAKE_JOB_POOL_LINK link)
endif()

if(RTS_BUILD_OPTION_ASAN)
    if(MSVC)
        set(ENV{ASAN_OPTIONS} "shadow_scale=2")
        add_compile_options(/fsanitize=address)
        add_link_options(/fsanitize=address)
    else()
        add_compile_options(-fsanitize=address)
        add_link_options(-fsanitize=address)
    endif()
endif()
